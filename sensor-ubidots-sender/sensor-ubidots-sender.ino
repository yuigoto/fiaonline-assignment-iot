/**
 * Retransmissor de dados de sensores BME-280 / MQ-2 para a nuvem
 * ----------------------------------------------------------------------
 * 
 * Assignment para a mat√©ria de "IOT e Conectividade", do curso de 
 * p√≥s-gradua√ß√£o em "Programa√ß√£o Fullstack e Desenvolvimento em 
 * Linguagens Eficientes" da FIA Online.
 * 
 * A ideia de utilizar v√°rias fun√ß√µes √© para facilitar a leitura e o 
 * entendimento de cada parte, fazendo com que cada uma tenha um 
 * prop√≥sito espec√≠fico.
 * 
 * Mais informa√ß√µes, ver `README.md`.
 * 
 * ----------------------------------------------------------------------
 * 
 * @author      Fabio Y. Goto <fabio.goto@gmail.com>
 * @version     1.0.0
 * @license     MIT
 */
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <ArduinoJson.h>
#include <AsyncTimer.h>
#include <ESP8266WiFi.h>
#include <SoftwareSerial.h>
#include <Ticker.h>
#include <Wire.h>
#include <Ubidots.h>
#include "pins_arduino.h"
#include "constants.h"
#include "credentials.h"

/* INST√ÇNCIAS 
 * ------------------------------------------------------------------- */

/**
 * Handler para display OLED.
 * 
 * Por padr√£o, utilizamos a resolu√ß√£o 128x64.
 */
Adafruit_SSD1306 OLED(OLED_H_RESOLUTION, OLED_V_RESOLUTION);

/**
 * Inst√¢ncia do geranciador de timer ass√≠ncrono.
 */
AsyncTimer ASYNC_TIMER;

/**
 * Handler para documento JSON a ser recebido.
 */
JsonDocument DOCUMENT;

/**
 * Inst√¢ncia de SoftwareSerial para recep√ß√£o de dados.
 */
SoftwareSerial SERIAL_HANDLER(SERIAL_RX_PIN, SERIAL_TX_PIN);

/**
 * Inst√¢ncia do cliente Ubidots, tamb√©m gerencia a conex√£o WiFi.
 */
Ubidots UBIDOTS(UBIDOTS_TOKEN, UBI_HTTP);

/* GLOBAIS 
 * ------------------------------------------------------------------- */

/**
 * Handler para erro de serializa√ß√£o de JSON recebido.
 */
DeserializationError JSON_SERIALIZE_ERROR; 

/**
 * Se os dados do sensor foram inicializados.
 */
boolean INITIALIZED = false;

/**
 * Buffer para o caractere sendo lido no momento.
 */
char READ_CHAR;

/**
 * Armazena em texto o conte√∫do do buffer de leitura serial para envio.
 */
String RECEIVED_DATA = "";

/**
 * Temperatura indicada pelo sensor.
 */
float SENSOR_TEMPERATURE = 0.0;

/**
 * Press√£o detectada pelo sensor.
 */
float SENSOR_PRESSURE = 0.0;

/**
 * Altitude indicada pelo sensor.
 */
float SENSOR_ALTITUDE = 0.0;

/**
 * Umidade indicada pelo sensor.
 */
float SENSOR_HUMIDITY = 0.0;

/**
 * N√≠vel de g√°s/fuma√ßa indicado pelo sensor.
 */
float SENSOR_GAS_LEVEL = 0.0;

/**
 * Limiar de n√≠vel de g√°s/fuma√ßa para detec√ß√£o.
 */
float SENSOR_GAS_LEVEL_THRESHOLD = 0.0;

/**
 * Se o g√°s/fuma√ßa foi detectado.
 */
boolean SENSOR_GAS_DETECTED = false;

/**
 * Buffer para armazenar os dados recebidos via serial.
 */
char SERIAL_BUFFER[512];

/**
 * √çndice do buffer de leitura serial.
 */
int SERIAL_BUFFER_INDEX = 0;

/**
 * Se o buffer de envio via serial foi enviado, utilizado para trigger 
 * inicial de envio para a Ubidots.
 */
boolean SERIAL_BUFFER_SENT = false;

/* LIFECYCLE 
 * ------------------------------------------------------------------- */

void setup() {
  initializeOLEDDisplay();
  delay(100);

  initializeSerial();
  delay(100);

  initializeSoftwareSerial();
  delay(100);

  awaitForSerialCommunication();
  delay(100);

  initializeUbidots();
  delay(100);

  ASYNC_TIMER.setTimeout(
    submitDataToUbidots,
    HTTP_REQUEST_INTERVAL_IN_SECONDS * 1000
  );
}

void loop() {
  captureAndParseData();

  ASYNC_TIMER.handle();
}

/* INICIALIZA√á√ÉO 
 * ------------------------------------------------------------------- */

/**
 * Aguarda pela inicializa√ß√£o da comunica√ß√£o serial, e imprime 
 * mensagem de inicializa√ß√£o.
 */
void awaitForSerialCommunication() {
  while (!Serial) {
    delay(100);
  }

  while (!SERIAL_HANDLER) {
    delay(100);
  }

  Serial.println("BME-280 / MQ-2 Data Sender");
  Serial.println("----------------------------------------");
  Serial.println("Author:  Fabio Y. Goto");
  Serial.println("Version: 1.0.0");
  Serial.println("----------------------------------------");
}

/**
 * Inicializa comunica√ß√£o com display OLED (opcional).
 */
void initializeOLEDDisplay() {
  pinMode(OLED_PIN_INPUT, INPUT);
  pinMode(OLED_PIN_OUTPUT, OUTPUT);

  // Inicializa a comunica√ß√£o com o display OLED no endere√ßo I2C 0x3C
  OLED.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  OLED.clearDisplay();
  OLED.setTextColor(WHITE);

  // Exibe mensagem de inicializa√ß√£o
  OLED.setTextSize(1);
  OLED.setCursor(0, 0);
  
  // Utiliza Extended ASCII
  OLED.cp437(true);

  OLED.println("BME-280/MQ-2 <-> WIFI");
  OLED.println("---------------------");
  OLED.println("Fabio Y. Goto || 2024");
  OLED.display();

  delay(2000);
  OLED.clearDisplay();
}

/**
 * Inicializa a comunica√ß√£o serial nativa, para debug via USB.
 */
void initializeSerial() {
  Serial.begin(SERIAL_SPEED);
  Serial.flush();
}

/**
 * Inicializa o necess√°rio para uso de `SoftwareSerial`.
 */
void initializeSoftwareSerial() {
  pinMode(SERIAL_RX_PIN, INPUT);
  pinMode(SERIAL_TX_PIN, OUTPUT);
  SERIAL_HANDLER.begin(SERIAL_SPEED);
  SERIAL_HANDLER.flush();
}

/**
 * Initializa a conex√£o com WiFi e servi√ßo Ubidots.
 */
void initializeUbidots() {
  UBIDOTS.wifiConnect(WIFI_SSID, WIFI_PASS);
  Serial.println("----------------------------------------");
  Serial.println("Connected to WiFi");
  Serial.println("----------------------------------------");
}

/* HELPERS 
 * ------------------------------------------------------------------- */

/**
 * Captura e analisa os dados recebidos via serial, para ent√£o executar 
 * as a√ß√µes necess√°rias.
 * 
 * Importante: por conta da captura cont√≠nua de dados, precisamos que 
 * todas as fun√ß√µes relacionadas √† impress√£o e envio de dados sejam 
 * executadas dentro do la√ßo.
 * 
 * Isso √© necess√°rio para garantir que os dados sejam devidamente 
 * impressos, sem perda de informa√ß√µes, uma vez que executar os m√©todos 
 * fora pode resultar em perda de dados.
 */
void captureAndParseData() {
  while (SERIAL_HANDLER.available() > 0) {
    READ_CHAR = SERIAL_HANDLER.read();
    SERIAL_BUFFER[SERIAL_BUFFER_INDEX++] = READ_CHAR;

    RECEIVED_DATA += READ_CHAR;

    if (READ_CHAR == '\n') {
      deserializeData();
      printDataToSerial();
      printDataToOLED();
      clearBuffer();
    }
  }
}

/**
 * Limpa o buffer de leitura serial.
 */
void clearBuffer() {
  RECEIVED_DATA = "";
  memset(SERIAL_BUFFER, 0, sizeof(SERIAL_BUFFER));
  SERIAL_BUFFER_INDEX = 0;
}

/**
 * Deserializa os dados recebidos em RECEIVED, armazenando em DOCUMENT.
 */
void deserializeData() {
  JSON_SERIALIZE_ERROR = deserializeJson(DOCUMENT, RECEIVED_DATA);

  if (JSON_SERIALIZE_ERROR) {
    Serial.print("Failed to deserialize JSON: ");
    Serial.println(JSON_SERIALIZE_ERROR.c_str());
  } else {
    INITIALIZED = true;
  }
}

/**
 * Imprime os dados do buffer serial no display OLED.
 */
void printDataToOLED() {
  SENSOR_TEMPERATURE          = DOCUMENT["temperature"];
  SENSOR_PRESSURE             = DOCUMENT["pressure"];
  SENSOR_ALTITUDE             = DOCUMENT["altitude"];
  SENSOR_HUMIDITY             = DOCUMENT["humidity"];
  SENSOR_GAS_LEVEL            = DOCUMENT["gas_level"];
  SENSOR_GAS_LEVEL_THRESHOLD  = DOCUMENT["gas_level_threshold"];
  SENSOR_GAS_DETECTED         = DOCUMENT["gas_detected"];
  
  OLED.clearDisplay();
  OLED.setCursor(0, 0);
  OLED.setTextSize(1);

  if (JSON_SERIALIZE_ERROR) {
    OLED.println("AWAITING DATA...");
  } else {
    OLED.println("PRESSURE: " + String(SENSOR_PRESSURE) + " hPa");
    OLED.print("TEMPERATURE: " + String(SENSOR_TEMPERATURE) + " ");
    OLED.print((char) 248); // ¬∞ üëàü´†
    OLED.println("C");
    OLED.println("HUMIDITY: " + String(SENSOR_HUMIDITY) + " %");
    OLED.println("ALTITUDE: " + String(SENSOR_ALTITUDE) + " m");
    OLED.println("GAS: " + String(SENSOR_GAS_LEVEL) + " ppm");
    OLED.println("THRESHOLD: " + String(SENSOR_GAS_LEVEL_THRESHOLD) + " ppm");
    OLED.print("LEAKAGE: ");
    if (SENSOR_GAS_DETECTED) {
      OLED.println("TRUE");
    } else {
      OLED.println("FALSE");
    }
  }
  OLED.display();
}

/**
 * Imprime os dados recebidos via serial.
 */
void printDataToSerial() {
  Serial.write(SERIAL_BUFFER, SERIAL_BUFFER_INDEX);
}

/**
 * Realiza o envio de dados recebidos no buffer para o servi√ßo da Ubidots.
 */
void submitDataToUbidots() {
  if (JSON_SERIALIZE_ERROR) {
    Serial.println("Failed to serialize JSON, skipping...");
  } else {
    UBIDOTS.add("temperature", SENSOR_TEMPERATURE);
    UBIDOTS.add("pressure", SENSOR_PRESSURE);
    UBIDOTS.add("altitude", SENSOR_ALTITUDE);
    UBIDOTS.add("humidity", SENSOR_HUMIDITY);
    UBIDOTS.add("gas_level", SENSOR_GAS_LEVEL);
    UBIDOTS.add("gas_level_threshold", SENSOR_GAS_LEVEL_THRESHOLD);
    UBIDOTS.add("gas_detected", SENSOR_GAS_DETECTED);
    UBIDOTS.send();
  }

  ASYNC_TIMER.setTimeout(
    submitDataToUbidots,
    HTTP_REQUEST_INTERVAL_IN_SECONDS * 1000
  );
}
